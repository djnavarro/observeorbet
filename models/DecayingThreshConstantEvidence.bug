model {
	
	# simple model: fixed thresholds
    for (s in 1:N) { # over all subjects
		threshold[s,1] ~ dgamma(thresh0_a,thresh0_b); # initial evidence threshold
		threshold[s,50] ~ dgamma(thresh1_a,thresh1_b); # final evidence threshold
		change[s] ~ dbeta(ch_a,ch_b)
		stepsize[s] <- (threshold[s,50]-threshold[s,1])/(1-change[s])
		
		for(t in 2:49) { # constant until trial 40
			threshold[s,t] <- threshold[s,1] + step((t/50) - change[s])*((t/50)-change[s])*stepsize[s]
		}
		
    }

	# evidence
	for( s in 1:N) {
		evidence[s,1] <- 0 # initial evidence is zero
		
		for(t in 2:50) { # over all trials
			evidence[s,t] <- (X[s,t-1] + evidence[s,t-1]); # evidence level on trial t
		}
	}
	
	# response
	eps <- .01
	for( s in 1:N) {
		sigma[s] ~ dexp(respstoch); # what is the noisiness
		for( t in 1:50) {
			lowertail[s,t] <- phi((-threshold[s,t] - evidence[s,t])/sigma[s]) # mass below the lower threshold
			uppertail[s,t] <- phi((-threshold[s,t] + evidence[s,t])/sigma[s]) # mass above the upper threshold
			betprob[s,t] <- min(1-eps,max(eps, lowertail[s,t] + uppertail[s,t]))
			
			A[s,t] ~ dbern(betprob[s,t]) # action sampled from Bernoulli distribution
		}
	}

	# --- researcher prior over the population distributions ---
	
	# group priors over response noise
	respstoch ~ dgamma(1,1);
	
	# group priors over threshold locations
	thresh0_a ~ dexp(1);
	thresh0_b ~ dexp(1);
	
	# change point prior
	ch_atemp ~ dgamma(1,1);
	ch_btemp ~ dgamma(1,1);
	ch_a <- 1+ch_atemp;
	ch_b <- 1+ch_btemp;
	
	# group priors over threshold locations
	thresh1_a ~ dexp(1);
	thresh1_b ~ dexp(1);
	
	
}
